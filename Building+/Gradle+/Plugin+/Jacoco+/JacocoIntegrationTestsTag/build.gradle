plugins {
    id 'java'
    id 'jacoco'
}
dependencies {
    def junitVersion = '6.0.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
            "org.junit.platform:junit-platform-launcher:$junitVersion"
}
repositories {
    mavenCentral()
}
tasks.register('testJacocoReport', JacocoReport) {
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
    executionData(file("$buildDir/build/jacoco/test.exec"))
}
tasks.register('integrationTestJacocoReport', JacocoReport) {
    reports {
        xml.required = true
        csv.required = true
        html {
            required = true
            outputLocation = layout.buildDirectory.dir('jacocoHtmlInteg')
        }
    }
    executionData(file("$buildDir/build/jacoco/integrationTest.exec"))
}
test {
    useJUnitPlatform {
        excludeTags 'integration'
    }
    finalizedBy testJacocoReport
}
tasks.register('integrationTest', Test) {
    shouldRunAfter test
    useJUnitPlatform {
        includeTags 'integration'
    }
    finalizedBy integrationTestJacocoReport
}
check.dependsOn integrationTest
