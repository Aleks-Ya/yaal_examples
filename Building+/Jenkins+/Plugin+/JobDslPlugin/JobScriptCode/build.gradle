apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs 'src/jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.jenkins-ci.org/releases/' }
}

configurations {
    testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
//configurations.all*.exclude group: 'xalan'

dependencies {
    def jobDslVersion = '1.77'
    def jenkinsVersion = '2.303.2'
    implementation 'org.codehaus.groovy:groovy-all:3.0.8'
    implementation "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"
    implementation 'org.kohsuke:github-api:1.133'

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'cglib:cglib-nodep:3.3.0' // used by Spock
    implementation 'org.connectbot:jbcrypt:1.0.2'

    // Jenkins test harness dependencies
    testImplementation('org.jenkins-ci.main:jenkins-test-harness:2.33') {
        exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane'
        // https://github.com/sheehan/job-dsl-gradle-example/issues/90
    }
    testImplementation("org.jenkins-ci.main:jenkins-war:${jenkinsVersion}") {
        exclude group: 'org.jenkins-ci.ui', module: 'bootstrap'
        // https://github.com/sheehan/job-dsl-gradle-example/issues/87
        exclude group: 'org.connectbot.jbcrypt', module: 'jbcrypt'
        // https://github.com/sheehan/job-dsl-gradle-example/issues/87
    }

    // Job DSL plugin including plugin dependencies
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testImplementation 'org.jenkins-ci.plugins:structs:1.13@jar'
    testImplementation 'org.jenkins-ci.plugins:cloudbees-folder:5.12@jar'
    testImplementation 'org.jenkins-ci.plugins:ghprb:1.40.0@jar'
    testImplementation 'org.jenkins-ci.plugins:credentials:2.1.10@jar'
    testImplementation 'com.coravy.hudson.plugins.github:github:1.29.0@jar'

    // plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.40.0'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:5.12'
    testPlugins 'org.jenkins-ci.plugins:credentials:2.1.10'

    // plugins used for auto-generated DSL. See example 9.
    testPlugins 'org.jenkins-ci.plugins:cvs:2.13'
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:2.7'

    // plugin dependencies
    testPlugins 'org.jenkins-ci.plugins:token-macro:2.5'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}
