subprojects {
    apply plugin: 'java'
    sourceSets.main.java.srcDir 'src'
    sourceSets.main.resources.srcDir 'resources'
    sourceSets.main.resources.srcDir 'resourcesTest'
    sourceSets.test.java.srcDir 'test'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    defaultTasks('test')
    repositories {
        mavenCentral()
    }
    ext {
        h2Dependency = 'com.h2database:h2:1.4.200'
        slf4jSimpleVersion = 'org.slf4j:slf4j-simple:1.8.0-beta4'
        def okHttpVersion = '4.9.1'
        okHttpDependency = "com.squareup.okhttp3:okhttp:$okHttpVersion"
        mockWebServerDependency = "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
        jettyServlet10Dependency = 'org.eclipse.jetty:jetty-servlet:10.0.0'
        jettyServlet11Dependency = 'org.eclipse.jetty:jetty-servlet:11.0.0'
        servlet4Dependency = 'javax.servlet:javax.servlet-api:4.0.1'
        jacksonVersion = '2.12.3'
        jsonUnitDependency = 'net.javacrumbs.json-unit:json-unit:2.24.0'
        mockitoVersion = '3.12.4'
        mockitoDependency = "org.mockito:mockito-core:$mockitoVersion"
        log4jCoreDependency = 'org.apache.logging.log4j:log4j-core:2.14.1'
        gsonDependency = 'com.google.code.gson:gson:2.8.7'
        commonsLangDependency = 'org.apache.commons:commons-lang3:3.12.0'
        wireMockDependency = 'com.github.tomakehurst:wiremock:2.27.2'
        jaxbApiDependency = 'javax.xml.bind:jaxb-api:2.3.1'
        elasticSearchVersion = '7.12.0'
        junit5Version = '5.8.1'
        spring5Version = '5.3.13'
        spring4Version = '4.3.30.RELEASE'
        spring3Version = '3.2.18.RELEASE'
        springBootVersion = '2.6.0'
        springCloudVersion = '3.0.3'
        hadoop2Version = '2.10.1'
        fabric8ioVersion = '5.6.0'
        feignVersion = '11.6'
        mongoDbDriverDependency = 'org.mongodb:mongodb-driver:3.12.10'
        hibernateEntityManagerDependency = 'org.hibernate:hibernate-entitymanager:5.6.2.Final'
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

/**
 * Add test dependencies to all projects (exclude JUnit5).
 */
configure(subprojects - project('UnitTest+:JUnit+:JUnit4').subprojects - project('UnitTest+:TestNG+').subprojects) {
    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version", 'org.hamcrest:hamcrest:2.2',
                'org.awaitility:awaitility:4.1.1', 'commons-io:commons-io:2.11.0', 'org.assertj:assertj-core:3.21.0'
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    }
    test {
        testLogging.showStandardStreams = true
        useJUnitPlatform()
    }
}

/**
 * Add Util project to all projects.
 */
configure(subprojects - project(':Util')) {
    dependencies {
        implementation project(':Util')
        testImplementation project(path: ':Util', configuration: 'tests')
    }
}
