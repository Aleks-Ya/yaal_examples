subprojects {
    apply plugin: 'java'
    sourceSets.main.java.srcDir 'src'
    sourceSets.main.resources.srcDir 'resources'
    sourceSets.main.resources.srcDir 'resourcesTest'
    sourceSets.test.java.srcDir 'test'
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    defaultTasks('test')
    repositories {
        mavenCentral()
    }
    ext {
        h2Dependency = 'com.h2database:h2:1.4.200'
        slf4jSimpleVersion = 'org.slf4j:slf4j-simple:1.8.0-beta4'
        okHttpVersion = '4.9.1'
        okHttpDependency = "com.squareup.okhttp3:okhttp:$okHttpVersion"
        mockWebServerDependency = "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
        jettyServlet10Dependency = 'org.eclipse.jetty:jetty-servlet:10.0.0'
        jettyServlet11Dependency = 'org.eclipse.jetty:jetty-servlet:11.0.0'
        servlet4Dependency = 'javax.servlet:javax.servlet-api:4.0.1'
        jacksonVersion = '2.12.2'
        jsonUnitDependency = 'net.javacrumbs.json-unit:json-unit:2.24.0'
        mockitoVersion = '3.9.0'
        mockitoDependency = "org.mockito:mockito-core:$mockitoVersion"
        log4jCoreDependency = 'org.apache.logging.log4j:log4j-core:2.14.1'
        elasticSearchVersion = '7.12.0'
        junit5Version = '5.7.1'
        spring5Version = '5.2.7.RELEASE'
        spring4Version = '4.3.11.RELEASE'
        spring3Version = '3.2.18.RELEASE'
    }
}

/**
 * Add test dependencies to all projects (exclude JUnit5).
 */
configure(subprojects - project('UnitTest+:JUnit+:JUnit4').subprojects) {
    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version", 'org.hamcrest:hamcrest:2.2',
                'org.awaitility:awaitility:4.0.3', 'commons-io:commons-io:2.8.0'
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    }
    test {
        testLogging.showStandardStreams = true
        useJUnitPlatform()
    }
}

/**
 * Add Util project to all projects.
 */
configure(subprojects - project(':Util')) {
    dependencies {
        implementation project(':Util')
        testImplementation project(path: ':Util', configuration: 'tests')
    }
    test {
        testLogging.showStandardStreams = true
    }
}
