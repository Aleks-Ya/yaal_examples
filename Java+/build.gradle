subprojects {
    apply plugin: 'java'
    sourceSets.main.java.srcDir 'src'
    sourceSets.main.resources.srcDir 'resources'
    sourceSets.main.resources.srcDir 'resourcesTest'
    sourceSets.test.java.srcDir 'test'
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    defaultTasks('test')
    test {
        testLogging.showStandardStreams = true
    }
    repositories {
        mavenCentral()
    }
    ext {
        h2Dependency = 'com.h2database:h2:1.4.200'
        slf4jSimpleVersion = 'org.slf4j:slf4j-simple:1.8.0-beta4'
        okHttpVersion = '4.9.1'
        okHttpDependency = "com.squareup.okhttp3:okhttp:$okHttpVersion"
        mockWebServerDependency = "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
        jettyServlet10Dependency = 'org.eclipse.jetty:jetty-servlet:10.0.0'
        jettyServlet11Dependency = 'org.eclipse.jetty:jetty-servlet:11.0.0'
        servlet4Dependency = 'javax.servlet:javax.servlet-api:4.0.1'
    }
}

/**
 * Add test dependencies to all projects (exclude JUnit5).
 */
configure(subprojects - project('UnitTest+:JUnit+:JUnit5+').subprojects) {
    dependencies {
        testImplementation 'junit:junit:4.13.2', 'org.hamcrest:hamcrest:2.2',
                'com.googlecode.catch-exception:catch-exception:1.2.0',
                'org.awaitility:awaitility:4.0.3', 'commons-io:commons-io:2.8.0'
    }
}

/**
 * Add Util project to all projects.
 */
configure(subprojects - project(':Util')) {
    dependencies {
        implementation project(':Util')
        testImplementation project(path: ':Util', configuration: 'tests')
    }
}
