subprojects {
    apply plugin: 'java'
    sourceSets.main.java.srcDir 'src'
    sourceSets.main.resources.srcDir 'resources'
    sourceSets.test.java.srcDir 'test'
    sourceSets.test.resources.srcDir 'resourcesTest'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    defaultTasks('test')
    repositories {
        mavenCentral()
    }
    ext {
        h2Dep = 'com.h2database:h2:2.1.214'
        postgresDep = 'org.postgresql:postgresql:42.2.8'
        derbyDep = 'org.apache.derby:derby:10.13.1.1'
        hsqldbDep = 'org.hsqldb:hsqldb:2.3.4'
        slf4jSimpleDep = 'org.slf4j:slf4j-simple:2.0.6'
        def okHttpVersion = '4.10.0'
        okHttpDep = "com.squareup.okhttp3:okhttp:$okHttpVersion"
        mockWebServerDep = "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
        jettyServlet10Dep = 'org.eclipse.jetty:jetty-servlet:10.0.0'
        jettyServlet11Dep = 'org.eclipse.jetty:jetty-servlet:11.0.0'
        servlet4Dep = 'javax.servlet:javax.servlet-api:4.0.1'
        jacksonVersion = '2.14.2'
        jacksonDataBindDep = "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        jacksonDatatypeJsr310Dep = "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
        jsonUnitDep = 'net.javacrumbs.json-unit:json-unit:2.36.1'
        mockitoVersion = '4.11.0'
        mockitoDep = "org.mockito:mockito-core:$mockitoVersion"
        mockitoJunitJupiterDep = "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        log4jCoreDep = 'org.apache.logging.log4j:log4j-core:2.14.1'
        logbackClassicDep = 'ch.qos.logback:logback-classic:1.2.11'
        gsonDep = 'com.google.code.gson:gson:2.10.1'
        commonsLangDep = 'org.apache.commons:commons-lang3:3.12.0'
        wireMockDep = 'com.github.tomakehurst:wiremock:2.27.2'
        jaxbApiDep = 'javax.xml.bind:jaxb-api:2.3.1'
        elasticSearchVersion = '7.12.0'
        junit5PlatformVersion = '1.9.2'
        junit5Version = '5.9.2'
        cucumberVersion = '7.11.1'
        spring5Version = '5.3.25'
        spring4Version = '4.3.30.RELEASE'
        spring3Version = '3.2.18.RELEASE'
        spring5TestDep = "org.springframework:spring-test:$spring5Version"
        springBoot2Version = '2.7.8'
        springBoot2StarterWebDep = "org.springframework.boot:spring-boot-starter-web:$springBoot2Version"
        springBoot2StarterDataJpaDep = "org.springframework.boot:spring-boot-starter-data-jpa:$springBoot2Version"
        springCloud3Version = '3.1.5'
        springSecurityVersion = '5.8.0'
        springBoot3Version = '3.0.2'
        springBoot3StarterWebDep = "org.springframework.boot:spring-boot-starter-web:$springBoot3Version"
        springBoot3StarterDataJpaDep = "org.springframework.boot:spring-boot-starter-data-jpa:$springBoot3Version"
        hadoop2Version = '2.10.1'
        hadoop3Version = '3.3.3'
        fabric8ioVersion = '5.6.0'
        feignVersion = '11.6'
        mongoDbDriverDep = 'org.mongodb:mongodb-driver:3.12.10'
        hibernate5Version = '5.6.12.Final'
        hibernate5EntityManagerDep = "org.hibernate:hibernate-entitymanager:$hibernate5Version"
        eclipseLink2Version = '2.7.11'
        eclipseLink2Dep = "org.eclipse.persistence:eclipselink:$eclipseLink2Version"
        eclipseLinkJpa2Dep = "org.eclipse.persistence:org.eclipse.persistence.jpa:$eclipseLink2Version"
        jsonAssertDep = 'org.skyscreamer:jsonassert:1.5.1'
        bouncyCustleDep = 'org.bouncycastle:bcpkix-jdk15on:1.68'
        testContainersVersion = '1.17.6'
        testContainersDep = "org.testcontainers:testcontainers:$testContainersVersion"
        testContainersJunitDep = "org.testcontainers:junit-jupiter:$testContainersVersion"
        testContainersPostgresDep = "org.testcontainers:postgresql:$testContainersVersion"
        redissonDep = 'org.redisson:redisson:3.17.4'
        lettuceDep = 'io.lettuce:lettuce-core:6.2.0.RELEASE'
        jedisDep = 'redis.clients:jedis:4.2.3'
        embeddedRedisOzimov = 'it.ozimov:embedded-redis:0.7.3'
        hikariCPDep = 'com.zaxxer:HikariCP:5.0.1'
        sqliteJdbcDep = 'org.xerial:sqlite-jdbc:3.40.0.0'
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

/**
 * Add test dependencies to all projects (exclude JUnit5).
 */
configure(subprojects - project('UnitTest+:JUnit+:JUnit4').subprojects - project('UnitTest+:TestNG+').subprojects) {
    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version", 'org.hamcrest:hamcrest:2.2',
                'org.awaitility:awaitility:4.2.0', 'commons-io:commons-io:2.11.0', 'org.assertj:assertj-core:3.24.1'
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    }
    test {
        testLogging.showStandardStreams = true
        useJUnitPlatform()
    }
}

/**
 * Add Util project to all projects.
 */
configure(subprojects - project(':Util')) {
    dependencies {
        implementation project(':Util')
        testImplementation project(path: ':Util', configuration: 'tests')
    }
}
