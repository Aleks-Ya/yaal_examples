subprojects {
    apply plugin: 'java'
    sourceSets.main.java.srcDir 'src'
    sourceSets.main.resources.srcDir 'resources'
    sourceSets.main.resources.srcDir 'resourcesTest'
    sourceSets.test.java.srcDir 'test'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    defaultTasks('test')
    test {
        testLogging.showStandardStreams = true
    }
    repositories {
        mavenCentral()
    }
    ext {
        h2Dependency = 'com.h2database:h2:1.4.197'
        slf4jSimpleVersion = 'org.slf4j:slf4j-simple:1.7.30'
    }
}

/**
 * Add test dependencies to all projects (exclude JUnit5).
 */
configure(subprojects - project('UnitTest+:JUnit+:JUnit5+').subprojects) {
    dependencies {
        testCompile 'junit:junit:4.12', 'org.hamcrest:java-hamcrest:2.0.0.0',
                'com.googlecode.catch-exception:catch-exception:1.2.0',
                'org.awaitility:awaitility:3.0.0', 'commons-io:commons-io:2.6'
    }
}

/**
 * Add Util project to all projects.
 */
configure(subprojects - project(':Util')) {
    dependencies {
        compile project(':Util')
        testCompile project(path: ':Util', configuration: 'tests')
    }
}
